apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion '30'
    defaultConfig {
        applicationId "com.tisza.tarock"
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 26
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "v${defaultConfig.versionName}${versionNameSuffix}"
            buildConfigField "String", 'SERVER_HOSTNAME', "\"dell\""
            buildConfigField "int", 'SERVER_PORT', "8128"
        }
        release {
            resValue "string", "app_version", "v${defaultConfig.versionName}"
            buildConfigField "String", 'SERVER_HOSTNAME', "\"tarokk.net\""
            buildConfigField "int", 'SERVER_PORT', "8128"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            proto {
                srcDir '../../proto'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        disable 'RtlHardcoded'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation 'com.facebook.android:facebook-android-sdk:5.0.1'
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.jakewharton:process-phoenix:2.0.0'
    testImplementation 'junit:junit:4.12'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.4"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
